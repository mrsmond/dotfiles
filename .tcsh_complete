# Needed by ssh
if ( ! $?hosts ) set hosts

foreach f ( "$HOME/."{,r,ssh/known_}hosts* \
  /usr/local/etc/csh.hosts /etc/hosts.equiv )
  if ( -r "$f" ) then
    set hosts=($hosts `sed \
      -e 's/#.*//' \
      -e '/^[+-]@/d' \
      -e 's/^[-+]//' \
      -e 's/[[:space:]].*//' \
      -e 's/,/\n/g' "$f" \
      | sed -e '/^[.:[:xdigit:][:space:]]*$/d'`)
  endif
end
unset f

if ( -r "$HOME/.netrc" ) then
  set hosts=($hosts `awk '$1 == "machine" { print $2 }' "$HOME/.netrc"`)
endif

set hosts=(`echo $hosts | tr ' ' '\012' | sort -u`)


# See https://github.com/tcsh-org/tcsh/blob/master/complete.tcsh
# directories
complete cd 'C/*/d/'
complete rmdir 'C/*/d/'
complete pushd 		p/1/d/
complete popd 		p/1/d/
complete u 		p/1/d/
complete o 		p/1/d/

# use available commands as arguments for which, where, and man
complete which 'p/1/c/'
complete where 'p/1/c/'
complete man 'p/1/c/'

# variables
complete unset 'p/1/s/'
complete set 'p/1/s/'

# environment variables
complete unsetenv 'p/1/e/'
complete setenv 'p/1/e/'

# groups
complete chgrp 'p/1/g/'

# users
complete chown 'p/1/u/'

complete ssh p/1/\$hosts/ \
             c/-/"(l n)"/ \
             'n/-l/u/ N/-l/c/ n/-/c/ p/2/c/ p/*/f/'

complete find \
n/-fstype/"(nfs 4.2)"/ \
n/-name/f/ \
n/-type/"(c b d f p l s)"/ \
n/-user/u/ \
n/-group/g/ \
n/-exec/c/ \
n/-ok/c/ \
n/-cpio/f/ \
n/-ncpio/f/ \
n/-newer/f/ \
c/-/"(fstype name perm prune type user nouser group nogroup size inum \
atime mtime ctime exec ok print ls cpio ncpio newer xdev depth \
daystart follow maxdepth mindepth noleaf version anewer cnewer \
amin cmin mmin true false uid gid ilname iname ipath iregex links \
lname empty path regex used xtype fprint fprint0 fprintf print0 \
printf not a and o or)"/ \
'n/*/d/'

complete -%*		c/%/j/			# fill in the jobs builtin
complete {fg,bg,stop}	'c/%/j/' 'p/1/"(%)"//'

complete svn \
  'n@ls@(file:/// svn+ssh:// svn://)@@' \
  'n@help@(add blame cat checkout cleanup commit copy \
delete export help import info list ls \
lock log merge mkdir move propdel propedit \
propget proplist propset resolve revert \
status switch unlock update)@' \
  'p@1@(add blame cat checkout cleanup commit copy \
delete export help import info list ls lock \
log merge mkdir move propdel propedit propget \
proplist propset resolved revert status switch \
unlock update)@'

complete grep	'c/-*A/x':'<#_lines_after>'/ 'c/-*B/x':'<#_lines_before>'/\
c/--/"(extended-regexp fixed-regexp basic-regexp \
regexp file ignore-case word-regexp line-regexp \
no-messages revert-match version help byte-offset \
line-number with-filename no-filename quiet silent \
text directories recursive files-without-match \
files-with-matches count before-context after-context \
context binary unix-byte-offsets)"/ \
c/-/"(A a B b C c d E e F f G H h i L l n q r s U u V \
v w x)"/ \
p/1/x:'<limited_regular_expression>'/ 'N/-*e/f/' \
'n/-*e/x':'<limited_regular_expression>'/ 'n/-*f/f/' 'n/*/f/'
